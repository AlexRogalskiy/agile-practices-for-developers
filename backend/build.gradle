//region plugins
plugins {
    id 'java'
    id 'application'
    id 'net.ltgt.apt' version '0.21' //lombok
    id 'org.springframework.boot' version '2.1.0.RELEASE'

    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.3.0'
    id 'maven-publish'
    id 'com.gradle.build-scan' version '2.0.2'
}
//endregion

//region repos
repositories {
    mavenCentral()
    maven {
        url 'https://ec2-3-120-32-70.eu-central-1.compute.amazonaws.com:8081/repository/maven-snapshots/'
        credentials {
            username 'admin'
            password '2019-04-raiffeisen'
        }
    }
}
//endregion

//region integration tests sourcepath
sourceSets {
    integrationTest {
        java {
            srcDir file('src/integrationTest/java')
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
        resources {
            srcDir file('src/integrationTest/resources')
        }
    }
}
//endregion

//region integration tests configuration
configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
    ui
}
//endregion

//region dependencies
dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.6'

    implementation 'org.codehaus.groovy:groovy-backports-compat23:2.5.4'
    implementation 'org.projectlombok:lombok:1.18.6'
    implementation 'org.springframework.boot:spring-boot-dependencies:2.1.0.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-web:2.1.0.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-security:2.1.0.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc:2.1.0.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-mail:2.1.0.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.1.0.RELEASE'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'org.slf4j:log4j-over-slf4j:1.7.25'
    implementation 'org.slf4j:jcl-over-slf4j:1.7.25'
    //region turn off freaky dependency
    components {
        withModule('org.springframework:spring-beans') {
            allVariants {
                withDependencyConstraints {
                    // Need to patch constraints because snakeyaml is an optional dependency
                    it.findAll { it.name == 'snakeyaml' }.each { it.version { strictly '1.19' } }
                }
            }
        }
    }
    //endregion

    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'

    implementation 'com.google.guava:guava:27.0-jre' //TODO remove
    implementation 'org.apache.commons:commons-lang3:3.8.1' // TODO remove

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.easytesting:fest-assert:1.4'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.1.0.RELEASE'
    testImplementation 'org.skyscreamer:jsonassert:1.5.0'
    testImplementation 'org.seleniumhq.selenium:selenium-java:3.141.59'
    testImplementation 'io.cucumber:cucumber-java:4.3.0'
    testImplementation 'io.cucumber:cucumber-spring:4.3.0'
    testImplementation 'io.cucumber:cucumber-junit:4.3.0'

    runtime 'org.liquibase:liquibase-core:3.6.2'
    runtime 'com.mattbertolini:liquibase-slf4j:2.0.0'
    runtime 'org.liquibase:liquibase-groovy-dsl:2.0.2'

    runtime 'org.apache.derby:derby:10.14.2.0'
    runtime 'postgresql:postgresql:9.1-901-1.jdbc4'
}
//endregion

//region pitest as part of test phase
test {
    exclude 'com/acme/dbo/cucumber/glue/**'

    pitest {
        threads = 4
        outputFormats = ['XML', 'HTML']
        timestampedReports = false
        jvmArgs = ['-Xmx1024m']
    }
}
//endregion

//region integration tests task
task integrationTest(type: Test) {
    description = 'Integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    outputs.upToDateWhen { false }
    mustRunAfter test
}

check.dependsOn integrationTest
//endregion

//region jar package phase
mainClassName = 'com.acme.dbo.Application'
//endregion

//region sources package phase
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}
//endregion

/* TODO fix javadoc generation
//region javadocs generation and package phase
javadoc {
    options.encoding = 'UTF-8'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}
//endregion
*/

//region repo publish phase
artifacts {
    archives distZip
}

group = 'com.acme.dbo'
version = '1.0-SNAPSHOT'
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'backend'
            from components.java
            artifact sourcesJar
//            artifact javadocJar
            artifact distZip
        }
    }
    repositories {
        maven {
            url 'https://ec2-3-120-32-70.eu-central-1.compute.amazonaws.com:8081/repository/maven-snapshots/'
            credentials {
                username 'admin'
                password '2019-04-raiffeisen'
            }
        }
    }
}
//endregion

//region build profile phase
buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
//    publishAlways() //TODO When stable
}
//endregion

defaultTasks 'clean', 'build', 'publish'
